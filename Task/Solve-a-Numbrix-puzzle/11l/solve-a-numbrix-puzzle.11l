V MOVES = [(1, 0), (0, 1), (-1, 0), (0, -1)]

T Numbrix
   [[Int]] grid
   [Int] clues
   Int totalToFill
   Int startRow
   Int startCol

   F solve(row, col, Int count, Int =nextClue) -> Bool
      I count > .totalToFill
         R 1B
      V back = .grid[row][col]
      I back !C [0, count]
         R 0B
      I back == 0 & nextClue < .clues.len & .clues[nextClue] == count
         R 0B
      nextClue = nextClue
      I back == count
         nextClue++
      .grid[row][col] = count
      L(move) :MOVES
         I .solve(row + move[1], col + move[0], count + 1, nextClue)
            R 1B
      .grid[row][col] = back
      R 0B

   F xprint()
      L(row) .grid
         L(val) row
            I val != -1
               print(f:‘{val:02} ’, end' ‘’)
         print()

F initNumbrix([String] board) -> Numbrix
   V result = Numbrix()
   V nRows = board.len
   V nCols = board[0].split(‘,’).len
   result.grid = [[-1] * (nCols + 2)] * (nRows + 2)
   result.totalToFill = nRows * nCols
   [Int] xlist
   L(row) board
      V r = L.index
      L(tile) row.split(‘,’)
         V c = L.index
         V val = Int(tile)
         result.grid[r + 1][c + 1] = val
         I val > 0
            xlist.append(val)
            I val == 1
               result.startRow = r + 1
               result.startCol = c + 1
   xlist.sort()
   result.clues = xlist
   R result

V EXAMPLE1 = [‘00,00,00,00,00,00,00,00,00’,
              ‘00,00,46,45,00,55,74,00,00’,
              ‘00,38,00,00,43,00,00,78,00’,
              ‘00,35,00,00,00,00,00,71,00’,
              ‘00,00,33,00,00,00,59,00,00’,
              ‘00,17,00,00,00,00,00,67,00’,
              ‘00,18,00,00,11,00,00,64,00’,
              ‘00,00,24,21,00,01,02,00,00’,
              ‘00,00,00,00,00,00,00,00,00’]
V EXAMPLE2 = [‘00,00,00,00,00,00,00,00,00’,
              ‘00,11,12,15,18,21,62,61,00’,
              ‘00,06,00,00,00,00,00,60,00’,
              ‘00,33,00,00,00,00,00,57,00’,
              ‘00,32,00,00,00,00,00,56,00’,
              ‘00,37,00,01,00,00,00,73,00’,
              ‘00,38,00,00,00,00,00,72,00’,
              ‘00,43,44,47,48,51,76,77,00’,
              ‘00,00,00,00,00,00,00,00,00’]
V EXAMPLE3 = [‘17,00,00,00,11,00,00,00,59’,
              ‘00,15,00,00,06,00,00,61,00’,
              ‘00,00,03,00,00,00,63,00,00’,
              ‘00,00,00,01,66,00,00,00,00’,
              ‘23,24,00,68,67,78,00,54,55’,
              ‘00,00,00,00,72,00,00,00,00’,
              ‘00,00,35,00,00,00,49,00,00’,
              ‘00,29,00,00,40,00,00,47,00’,
              ‘31,00,00,00,39,00,00,00,45’]

L(board) [EXAMPLE1, EXAMPLE2, EXAMPLE3]
   V numbrix = initNumbrix(board)
   I numbrix.solve(numbrix.startRow, numbrix.startCol, 1, 0)
      print(‘Solution for example ’(L.index + 1)‘:’, end' ‘’)
      numbrix.xprint()
   E
      print(‘No solution.’)
