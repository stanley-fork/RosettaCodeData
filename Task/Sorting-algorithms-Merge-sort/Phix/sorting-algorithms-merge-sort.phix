with javascript_semantics
function merge(sequence left, right)
    sequence result = {}
    integer ldx = 1, rdx = 1
    while length(left)>=ldx
      and length(right)>=rdx do
        if left[ldx]<=right[rdx] then
            result = append(result, left[ldx])
            ldx += 1
        else
            result = append(result, right[rdx])
            rdx += 1
        end if
    end while
    return result & left[ldx..$] & right[rdx..$]
end function

function mergesort(sequence m)
    integer middle = floor(length(m)/2)
    if middle=0 then return m end if
    sequence left = mergesort(m[1..middle]),
            right = mergesort(m[middle+1..$])
    return iff(left[$]<=right[1]?left & right:
           iff(right[$]<=left[1]?right & left:
                            merge(left, right)))
end function

constant s = shuffle(tagset(10))
? s
? mergesort(deep_copy(s))
