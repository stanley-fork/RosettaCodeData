import extensions;
import system'math;
import system'routines;

extension op
{
    cocktailSort()
    {
        var list := self.clone();

        bool swapped  := true;
        while(swapped)
        {
            swapped := false;

            for(int i := 0; i <= list.Length - 2; i += 1)
            {
                if (list[i]>list[i+1])
                {
                    list.exchange(i,i+1);
                    swapped := true
                }
            };
            if:not (swapped)
            {
                ^ list
            };
            swapped := false;

            for(int i := list.Length - 2; i >= 0; i -= 1)
            {
                if (list[i]>list[i+1])
                {
                    list.exchange(i,i+1);
                    swapped := true
                }
            }
        };

        ^ list
    }
}

public Program()
{
    var list := new int[]{3, 5, 1, 9, 7, 6, 8, 2, 4 };

    Console.printLine("before:", list.asEnumerable());
    Console.printLine("after :", list.cocktailSort().asEnumerable())
}
