function poly(sequence si) -- display helper
    string r = ""
    integer l = length(si)
    for t=1 to l do
        atom sit = si[t]
        if sit!=0 then
            if sit=1 and t<l then
                r &= iff(r=""? "":" + ")
            elsif sit=-1 and t<l then
                r &= iff(r=""?"-":" - ")
            else
                if r!="" then
                    r &= iff(sit<0?" - ":" + ")
                    sit = abs(sit)
                end if
                r &= sprintf("%g",sit)
            end if
            r &= iff(t<l?"x"&iff(t<l-1?sprintf("^%d",l-t):""):"")
        end if
    end for
    if r="" then r="0" end if
    return r
end function

procedure solve_quadratic(sequence t3)
    atom {a,b,c} = t3,
         d = b*b-4*a*c, f
    string t
    sequence u
    if abs(d)<1e-6 then d=0 end if
    switch sign(d) do
        case 0: t = "single root is %g"
                u = {-b/2/a}
        case 1: t = "real roots are %g and %g"
                f = (1+sqrt(1-4*a*c/(b*b)))/2
                u = {-f*b/a,-c/b/f}
        case-1: t = "complex roots are %g +/- %g*i"
                u = {-b/2/a,sqrt(-d)/2/a}
    end switch
    printf(1,"%25s : the %s\n",{poly(t3),sprintf(t,u)})
end procedure

constant tests = {{1,-1E9,1},
                  {1,0,1},
                  {2,-1,-6},
                  {1,2,-2},
                  {0.5,1.4142135,1},
                  {1,3,2},
                  {3,4,5},
                  {1,-2,1},
                  {1,-10,1},
                  {1,-1000,1}}

papply(tests,solve_quadratic)
