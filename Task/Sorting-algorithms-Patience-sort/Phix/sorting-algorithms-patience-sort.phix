with javascript_semantics

function patience_sort(sequence s)
    -- create list of sorted lists
    sequence piles = {}
    for i,n in s do
        for p=length(piles) to 0 by -1 do
            if p=0 then
                piles = append(piles,{n})
            elsif n>=piles[p][$] then
                piles[p] = append(piles[p],n)
                exit
            end if
        end for
    end for
    -- merge sort the piles
    sequence res = ""
    while length(piles)>1 do
        integer idx = smallest(piles,return_index:=true)
        res = append(res,piles[idx][1])
        if length(piles[idx])=1 then
            piles[idx..idx] = {}
        else
            piles[idx] = piles[idx][2..$]
        end if
    end while
    if length(piles)=1 then res &= piles[1] end if
    return res
end function

constant tests = {{4,65,2,-31,0,99,83,782,1},
                  {0,8,4,12,2,10,6,14,1,9,5,13,3,11,7,15},
                  "nonzerosum",
                  {"dog", "cow", "cat", "ape", "ant", "man", "pig", "ass", "gnu"}}

for t in tests do
    pp(patience_sort(t),{pp_IntCh,false})
end for
