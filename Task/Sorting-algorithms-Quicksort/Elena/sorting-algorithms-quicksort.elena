import extensions;
import system'routines;
import system'collections;

extension op
{
    quickSort()
    {
        if (self.isEmpty()) { ^ self };

        var pivot := self[0];

        auto less := new ArrayList();
        auto pivotList := new ArrayList();
        auto more := new ArrayList();

        self.forEach::(item)
        {
            if (item < pivot)
            {
                less.append(item)
            }
            else if (item > pivot)
            {
                more.append(item)
            }
            else
            {
                pivotList.append(item)
            }
        };

        less := less.quickSort();
        more := more.quickSort();

        less.appendRange(pivotList);
        less.appendRange(more);

        ^ less
    }
}

public Program()
{
    var list := new int[]{3, 14, 1, 5, 9, 2, 6, 3};

    Console.printLine("before:", list.asEnumerable());
    Console.printLine("after :", list.quickSort().asEnumerable());
}
