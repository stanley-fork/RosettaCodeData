with javascript_semantics
requires("1.0.2") -- (join(fmt))
function partition(integer v, n, idx=0)
    if n=1 then
        return iff(is_prime(v)?{v}:0)
    end if
    while true do
        idx += 1
        integer np = get_prime(idx)
        if np>=floor(v/2) then exit end if
        object res = partition(v-np, n-1, idx)
        if sequence(res) then
            res = prepend(res,np)
            return res
        end if
    end while
    return 0
end function

constant tests = {{99809, 1},
                  {18, 2},
                  {19, 3},
                  {20, 4},
                  {2017, 24},
                  {22699, 1},
                  {22699, 2},
                  {22699, 3},
                  {22699, 4},
                  {40355, 3}}

for i=1 to length(tests) do
    integer {v,n} = tests[i]
    object res = partition(v,n)
    string sep = iff(n<10?" + ":"+")
    res = iff(res=0?"not possible":join(res,sep,fmt:="%d"))
    printf(1,"Partition %d into %d primes: %s\n",{v,n,res})
end for
