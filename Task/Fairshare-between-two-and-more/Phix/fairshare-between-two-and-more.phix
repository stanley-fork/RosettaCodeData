with javascript_semantics
function fairshare(integer n, base)
    sequence res = repeat(0,n)
    for i=1 to n do
        integer j = i-1, t = 0
        while j>0 do
            t += rmdr(j,base)
            j = floor(j/base)
        end while
        res[i] = rmdr(t,base)
    end for
    return {base,res}
end function

for t in {2,3,5,11} do
    printf(1,"%2d : %v\n",fairshare(25,t))
end for

function turns(integer base)
    sequence c = repeat(0,base)
    for u in fairshare(50000,base)[2] do
        c[u+1] += 1
    end for
    if find(0,c) then return sprintf("only %d have a turn",sum(c)) end if
    return join(unique(c)," or ",fmt:="%d")
end function

printf(1,"\nHow many times does each get a turn in 50,000 iterations?\n")
for base in {191, 1377, 49999, 50000, 50001} do
    printf(1,"  With %5d people: %s\n",{base,turns(base)})
end for
