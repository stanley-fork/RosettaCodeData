with javascript_semantics
function to_snake_case(string s)
    string snake = substitute(trim(s)," ","_")
    for i=length(snake) to 1 by -1 do
        if isupper(snake[i]) then
            snake[i..i] = '_'&lower(snake[i])
        end if
    end for
    return snake
end function

function toCamelCase(string s)
    string camel = substitute_all(trim(s),"- ","__")
    for i=length(camel)-1 to 1 by -1 do
        if camel[i]='_' then
            camel[i..i+1] = upper(camel[i+1..i+1])
        end if
    end for
    return camel
end function

constant tests = {"snakeCase", "snake_case", "variable_10_case", "variable10Case", "ergo rE tHis",
                  "hurry-up-joe!", "c://my-docs/happy_Flag-Day/12.doc", "  spaces  "}
procedure test(string title, integer fn)
    printf(1,title)
    for i=1 to length(tests) do
        printf(1,"%33s ===> %s\n", {tests[i], fn(tests[i])})
    end for
end procedure
test("                          === to_snake_case ===\n",to_snake_case)
test("\n                          === toCamelCase ===\n",toCamelCase)
