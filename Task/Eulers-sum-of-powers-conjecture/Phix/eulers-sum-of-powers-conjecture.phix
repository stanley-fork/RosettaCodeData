with javascript_semantics
constant MAX = 250

constant p5 = new_dict(),
         sum2 = new_dict()

atom t0 = time()
for i=1 to MAX do
    atom i5 = power(i,5)
    setd(i5,i,p5)
    for j=1 to i-1 do
        atom j5 = power(j,5)
        setd(j5+i5,{i,j},sum2)
    end for
end for

printf(1,"created %d + %d dictionary entries (%gs)\n",
         {dict_size(p5),dict_size(sum2),time()-t0})

bool found = false
function forsum2(object s, data, p)
    if p<=s then return 0 end if
    integer k = getd_index(p-s,sum2)
    if k!=NULL then
        printf(1,"%d^5 + %d^5 + %d^5 +%d^5 = %d^5 (%gs)\n",
            getd_by_index(k,sum2)&data&getd(p,p5)&(time()-t0))
        found = true
        return 0 -- (show one solution per p)
    end if
    return 1
end function

function forp5(object key, /*data*/, /*user_data*/)
    traverse_dict(forsum2,key,sum2)
    return not found
end function

traverse_dict(forp5,0,p5)
