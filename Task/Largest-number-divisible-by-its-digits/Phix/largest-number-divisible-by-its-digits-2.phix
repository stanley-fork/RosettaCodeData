with javascript_semantics
integer r = 0, base = 16, size = 15,
        iterations = 0,
        com_div = lcm(tagset(size))
for i=size to 1 by -1 do
    r = rmdr(r*base+i,com_div)
end for
sequence n = tagset(size)
do
    integer m = r
    for i,d in n do
        d -= rmdr(m,base)
        m = floor(m/base)
        if d<0 then
            d += base
            m += 1
        end if
        n[i] = d
        if m=0 then exit end if
    end for
    r = com_div
    iterations += 1
    sequence seen = repeat(false,size)
    bool bad = false
    for d in n do
        bad = d=0 or seen[d]
        if bad then exit end if
        seen[d] = true
    end for
until not bad
n = join(extract("123456789abcdef",reverse(n)),"")
printf(1,"%s (%d iterations)\n",{n,iterations-1})
