function create_sieve(integer limit)
    sequence sieve = repeat(true,limit)
    sieve[1] = false
    for i=4 to limit by 2 do
        sieve[i] = false
    end for
    for p=3 to floor(sqrt(limit)) by 2 do
        integer p2 = p*p
        if sieve[p2] then
            for k=p2 to limit by p*2 do
                sieve[k] = false
            end for
        end if
    end for
    return sieve
end function

constant lim = 1000035,
--constant lim = 100, -- (this works too)
         limit = lim-(and_bits(lim,1)=0),   -- (limit must be odd)
         sieve = create_sieve(limit+6)  -- (+6 to check for sexiness)

sequence sets = repeat({},5),   -- (unsexy,pairs,trips,quads,quins)
         limits = {10,5,4,3,1},
         counts = 1&repeat(0,4) -- (2 is an unsexy prime)
integer  total = 1              -- ""

for i=limit to 3 by -2 do       -- (this loop skips 2)
    if sieve[i] then
        total += 1
        if sieve[i+6]=false and (i-6<0 or sieve[i-6]=false) then
            counts[1] += 1 -- unsexy
            if length(sets[1])<limits[1] then
                sets[1] = prepend(sets[1],i)
            end if
        else
            sequence set = {i}
            for j=i-6 to 3 by -6 do
                if j<=0 or sieve[j]=false then exit end if
                set = prepend(set,j)
                integer l = length(set)
                if length(sets[l])<limits[l] then
                    sets[l] = prepend(sets[l],set)
                end if
                counts[l] += 1
            end for
        end if
    end if
end for
if length(sets[1])<limits[1] then
    sets[1] = prepend(sets[1],2) -- (as 2 skipped above)
end if

constant fmt = """
Of %,d primes less than %,d there are:
%,d unsexy primes, the last %d being %s
%,d pairs, the last %d being %s
%,d triplets, the last %d being %s
%,d quadruplets, the last %d being %s
%,d quintuplet, the last %d being %s
"""
sequence results = {total,lim,
                    0,0,"",
                    0,0,"",
                    0,0,"",
                    0,0,"",
                    0,0,""}
for i=1 to 5 do
    results[i*3..i*3+2] = {counts[i],length(sets[i]),sprint(sets[i])}
end for
printf(1,fmt,results)
