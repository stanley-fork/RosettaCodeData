var [const] BI = Import.lib("zklBigNum");  // GMP
var kRE,kD, aRE,aD;

kRE,kD = ki();          // regular expression & hash table
aRE,aD = abrevCreate(); // ""

   // "288Doz" --> 3,456, "+1.567k" : 1,567.00
fcn naSuffixes(numStr){ // --> Float or BigInt
   var [const]          // aka static, ie allocated only once
     numRE  = RegExp(0'^([+-]*\.*\d+[.]*\d*E*[+-]*\d*)^),
     bangRE = RegExp(0'^(!+)^);

   nstr := (numStr - " ,").toUpper();
   numRE.search(nstr);
   nstr,r := nstr[numRE.matched[0][1],*], numRE.matched[1];
   if(r.matches("*[.E]*")) r = r.toFloat();  // don't have big float
   else                    r = BI(r);

    do{
      z := nstr; // use this to see if we actually did anything
      if(aRE.search(nstr)){
         ns,k := aRE.matched;   // ((0,3),"SCO")
         re,b := aD[k];         // (RegExp("R|RE|RES"),BI(20)),
         nstr  = nstr[ns[1],*];
         if(re.search(nstr))    // remove abbrev tail
            nstr = nstr[re.matched[0][1],*];
         r = r*b;
         continue;
      }else if(kRE.search(nstr)){
         r   *= kD[kRE.matched[1]];         // "K":1000 ...
         nstr = nstr[kRE.matched[0][1],*];
         continue;
      }else if(bangRE.search(nstr)){  // floats are converted to int
         n,k,z := r.toInt(), bangRE.matched[0][1], n - k;
         r,nstr = BI(n), nstr[k,*];
         while(z>0){ r.mul(z); z-=k; }
         continue;
      }
   }while(nstr and z!=nstr);
   r;
}

fcn ki{  // case insensitive: k, ki,..  --> RE & Dictionary/hash
   ss := "K M G T P E Z Y X W V U".split();
   d  := Dictionary(); // hash: ("YI":1208925819614629174706176 ...)
   ss.zipWith(d.add,[3..3*(ss.len()),3].apply(BI(10).pow));    // E:1e+18
   ss.apply("append","I")               // EI:1.15292e+18
      .zipWith(d.add,[10..10*(ss.len()),10].apply(BI(2).pow));
   re := "([%s]I?)".fmt(ss.concat());   // "([KMGTPEZYXWVU]I?)"
   return(RegExp(re),d);
}
fcn abrevCreate{        // --> RE & Dictionary/hash
   var upDown = RegExp("([A-Z]+)(.*)");
   s := "PAIRs 2; SCOres 20; DOZens 12; GREATGRoss 1728; GRoss 144; GOOGOLs 10e100"
        .split(";");
   abrevs,re := Dictionary(), Sink(String);
   foreach an in (s){
      a,n := an.split();
      upDown.search(a);
      u,d := upDown.matched[1,2];
      d = d.len().pump(List,  // "R|RE|RES"
                       '+(1),d.get.fp(0),"toUpper").reverse().concat("|");
         // --> ("DOZ" : (RegExp("ENS|EN|E"),12)..)
      abrevs.add(u,T(RegExp(d),BI(n)));
      re.write(u," ");
   }
     // --> RegExp("PAIR|SCO|DOZ|GR|GREATGR|GOOGOL")
   re = RegExp("(%s)".fmt(re.close().strip().replace(" ","|")));
   return(re,abrevs);
}
