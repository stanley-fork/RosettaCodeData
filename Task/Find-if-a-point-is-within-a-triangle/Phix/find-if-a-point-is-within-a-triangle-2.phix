with javascript_semantics
constant p0 = {0,0},
         p1 = {0,1},
         p2 = {3,1},
         triangle = {{3/2, 12/5}, {51/10, -31/10}, {-19/5, 1/2}}

function side(sequence p1, p2, p3)
    -- which side of plane cut by line (p2, p3) is p1 on?
    atom {x1, y1} = p1,
         {x2, y2} = p2,
         {x3, y3} = p3
    return (x1 - x3) * (y2 - y3) - (x2 - x3) * (y1 - y3)
end function

function iswithin(sequence point, triangle)
--
--  Determine if point is within triangle.
--  If so, the point will be on the same side of each of the half planes
--  defined by vectors p1p2, p2p3, and p3p1. side is positive if outside,
--  negative if inside such a plane. All should be non-negative or all
--  non-positive if the point is within the triangle.
--
    sequence {pt1, pt2, pt3} = triangle
    atom side12 = side(point, pt1, pt2),
         side23 = side(point, pt2, pt3),
         side31 = side(point, pt3, pt1)
    bool all_non_neg = side12 >= 0 and side23 >= 0 and side31 >= 0,
         all_non_pos = side12 <= 0 and side23 <= 0 and side31 <= 0
    return all_non_neg or all_non_pos
end function

printf(1,"point %v is with triangle %v?:%t\n",{p0,triangle,iswithin(p0,triangle)})
printf(1,"point %v is with triangle %v?:%t\n",{p1,triangle,iswithin(p1,triangle)})
printf(1,"point %v is with triangle %v?:%t\n",{p2,triangle,iswithin(p2,triangle)})
