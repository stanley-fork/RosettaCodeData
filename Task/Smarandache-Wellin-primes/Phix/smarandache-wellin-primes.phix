requires(64,true) -- (~four times faster than 32-bit)
requires("1.0.6") -- mpz_probab_prime_p
include mpfr.e
string sw = ""
mpz n = mpz_init(), dn = mpz_init()
sequence swp = {}, swdp = {}, dc = repeat(0,10)
integer np = 0
atom t0 = time(), t1 = time()+1
while length(swp)<iff(platform()=JS?5:8) do
    np += 1
    integer p = get_prime(np)
    string sp = sprintf("%d",p)
    sw &= sp
    mpz_set_str(n,sw,10)
    if mpz_probab_prime_p(n,1) then
        swp = append(swp,{np,p,shorten(sw)})
    end if
    for c in sp do
        dc[c-'0'+1] += 1
    end for
    integer dc9 = dc[10]
    if odd(dc9) and remainder(dc9,5)!=0 then
        string swdps = join(dc,"",fmt:="%d")
        mpz_set_str(dn,swdps,10)
        if mpz_prime(dn) then
            swdp = append(swdp,{np,swdps})
        end if
    end if
    if time()>t1 then
        progress("processing prime %d...\r",{np})
        t1 = time()+1
    end if
end while
printf(1,"Smarandache-Wellin primes:\n")
for i,s in swp do
    printf(1," %d%s:",{i,ord(i)})
    printf(1," Index: %4d, Last prime %5d, %s\n",s)
end for
printf(1,"Smarandache-Wellin derived primes:\n")
for i,s in swdp do
    printf(1," %d%s:",{i,ord(i)})
    printf(1," Index: %4d, %s\n",s)
end for
?elapsed(time()-t0)
