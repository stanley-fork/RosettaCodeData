wboot	equ	0	; jump to BIOS warm boot routine
bdos	equ 	5	; BDOS entry
conout	equ 	2	; BDOS console output function
putstr	equ	9	; BDOS print string function
ndoors	equ	100	; number of doors
	;
	org	100h
	lxi	sp,stack  ; set our own stack
	lxi	d,intro	  ; print introductory message
	mvi	c,putstr
	call	bdos
	;
	; generate sequence of squares to specified limit
	; HL holds the current square
	; DE holds the increment
	; BC holds the negative of the limit
	;
gensqr:	lxi	h,1	; starting value of square
	lxi	d,3	; starting value of increment
	lxi	b,-ndoors
sqrs2:	call	putdec	; otherwise print current square
	mvi	a,' '	; separate with a space
	call	putchr
	push	h	; have we reached the limit?
	dad	b
	pop	h
	jc	done	; CY if HL > limit
	dad	d	; square += incrememnt
	inx	d	; increment += 2
	inx	d
	jmp	sqrs2	; repeat until finished
	;
done:	jmp	wboot	; exit with warm boot
	;---------------------------------------------------
	; console output of char in A register
	;---------------------------------------------------
putchr:	push	h
	push	d
	push	b
	mov	e,a
	mvi	c,conout
	call	bdos
	pop	b
	pop	d
	pop	h
	ret
	;---------------------------------------------------
	; Output decimal number to console
	; HL holds 16-bit unsigned binary number to print
	;---------------------------------------------------
putdec: push	b
	push	d
	push	h
	lxi	b,-10
	lxi	d,-1
putdec2:
	dad	b
	inx	d
	jc	putdec2
	lxi	b,10
	dad	b
	xchg
	mov	a,h
	ora	l
	cnz	putdec		; recursive call
	mov	a,e
	adi	'0'
	call	putchr
	pop	h
	pop	d
	pop	b
	ret
	;---------------------------------------------------
	; data area and stack
	;---------------------------------------------------
intro: db	'The open doors are: $'
stack	equ	$+128		; 64-level stack
	;
	end
