with javascript_semantics
include mpfr.e

function isqrt(mpz x)
    if mpz_cmp_si(x,0)<0 then
        crash("Argument cannot be negative.")
    end if
    mpz q = mpz_init(1),
        r = mpz_init(0),
        t = mpz_init(),
        z = mpz_init_set(x)
    while mpz_cmp(q,x)<= 0 do
        mpz_mul_si(q,q,4)
    end while
    while mpz_cmp_si(q,1)>0 do
        assert(mpz_fdiv_q_ui(q, q, 4)=0)
        mpz_sub(t,z,r)
        mpz_sub(t,t,q)
        assert(mpz_fdiv_q_ui(r, r, 2)=0)
        if mpz_cmp_si(t,0) >= 0 then
            mpz_set(z,t)
            mpz_add(r,r,q)
        end if
    end while
    string star = iff(mpz_cmp_si(z,0)=0?"*":" ")
    return shorten(mpz_get_str(r,10,true))&star
end function

printf(1,"The integer square roots of integers from 0 to 65 are:\n")
for i=0 to 65 do
    printf(1,"%s ", {trim(isqrt(mpz_init(i)))})
end for
printf(1,"\n\npower %24s 7 ^ power %45s integer square root\n",{" "," "})
printf(1,"-----  %s   %s\n",{repeat('-',57),repeat('-',58)})
mpz pow7 = mpz_init(7)
for i=1 to 9000 do
    if (i<=73  and remainder(i,2)=1)
    or (i<100  and remainder(i,10)=5)
    or (i<1000 and remainder(i,100)=0)
    or             remainder(i,1000)=0 then
        printf(1,"%4d  %58s %61s\n", {i, shorten(mpz_get_str(pow7,10,true)),isqrt(pow7)})
    end if
    mpz_mul_si(pow7,pow7,7)
end for
