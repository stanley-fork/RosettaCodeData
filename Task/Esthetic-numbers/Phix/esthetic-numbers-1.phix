with javascript_semantics
constant aleph = "0123456789ABCDEF"

function efill(string s, integer ch, i)
    -- min-fill, like 10101 or 54321 or 32101
    s[i] = ch
    for j=i+1 to length(s) do
        ch = iff(ch>='1'?iff(ch='A'?'9':ch-1):'1')
        s[j] = ch
    end for
    return s
end function

function esthetic(string s, integer base = 10)
    -- generate the next esthetic number after s
    -- the special case of s="" always yields "1"
    -- (nb otherwise s is assumed to be esthetic)
    for i=length(s) to 1 by -1 do
        integer ch = s[i], cp = iff(i>1?s[i-1]:'0')
        if ch<cp and cp<aleph[base] then
            return efill(s,aleph[find(ch,aleph)+2],i)
        elsif i=1 and ch<aleph[base] then
            return efill(s,iff(ch='9'?'A':ch+1),i)
        end if
    end for
    return efill("1"&s,'1',1)
end function

string s
sequence res
for base=2 to 16 do
    integer hi = base*6,
            lo = base*4
    {s,res} = {"",{}}
    for i=1 to hi do
        s = esthetic(s, base)
        if i>=lo then
            res = append(res,s)
        end if
    end for
    res = join(shorten(res,"numbers",4))
    printf(1,"Base %d esthetic numbers[%d..%d]: %s\n",{base,lo,hi,res})
end for

{s,res} = {efill("1000",'1',1),{}}
while length(s)=4 do
    res = append(res,s)
    s = esthetic(s)
end while
res = {join(shorten(res,"numbers",5))}
printf(1,"\nBase 10 esthetic numbers between 1,000 and 9,999: %s\n\n",res)

function comma(string s)
    for i=length(s)-2 to 2 by -3 do
        s[i..i-1] = ","
    end for
    return s
end function

for k=7 to 19 by 3 do
    string f = "10"&repeat('0',k),
           t = "13"&repeat('0',k)
    {s,res} = {efill(f,'1',1),{}}
    while s<t do
        res = append(res,s)
        s = esthetic(s)
    end while
    res = join(shorten(res,"numbers",1))
    printf(1,"Base 10 esthetic numbers between %s and %s: %s\n",
             {comma(f),comma(t),res})
end for
