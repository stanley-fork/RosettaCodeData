with javascript_semantics

function radixSortn(sequence s, integer n)
    sequence buckets = repeat({},10), res = {}
    for i,si in s do
        integer digit = remainder(floor(si/power(10,n-1)),10)+1
        buckets[digit] = append(buckets[digit],si)
    end for
    for b in buckets do
        res &= iff(length(b)<=1 or n=1?b:radixSortn(b,n-1))
    end for
    return res
end function

function split_by_sign(sequence s)
    sequence buckets = {{},{}}
    for i=1 to length(s) do
        integer si = s[i], bn = 1+(si>=0)
        buckets[bn] = append(buckets[bn],abs(si))
    end for
    return buckets
end function

function radixSort(sequence s)
    integer mins = min(s),
            passes = max(max(s),abs(mins))
    passes = floor(log10(passes))+1
    if mins<0 then
        sequence buckets = split_by_sign(s)
        s = reverse(sq_uminus(radixSortn(buckets[1],passes)))
                            & radixSortn(buckets[2],passes)
    else
        s = radixSortn(s,passes)
    end if
    return s
end function

?radixSort({1, 3, 8, 9, 0, 0, 8, 7, 1, 6})
?radixSort({170, 45, 75, 90, 2, 24, 802, 66})
?radixSort({170, 45, 75, 90, 2, 24, -802, -66})
?radixSort({100000, -10000, 400, 23, 10000})
