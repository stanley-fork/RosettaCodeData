with javascript_semantics
function abundantOdd(integer n, done, lim, bool printAll)
    while done<lim do
        atom tot = sum(factors(n,-1))
        if tot>n then
            done += 1
            if printAll or done=lim then
                string ln = iff(printAll?sprintf("%2d. ",done):"")
                printf(1,"%s%,6d (proper sum:%,d)\n",{ln,n,tot})
            end if
        end if
        n += 2
    end while
    printf(1,"\n")
    return n
end function
printf(1,"The first 25 abundant odd numbers are:\n")
integer n = abundantOdd(1, 0, 25, true)
printf(1,"The one thousandth abundant odd number is:")
{} = abundantOdd(n, 25, 1000, false)
printf(1,"The first abundant odd number above one billion is:")
{} = abundantOdd(1e9+1, 0, 1, false)
