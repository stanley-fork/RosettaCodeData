with javascript_semantics

include builtins/bigatom.e

constant suffixes = "KMGTPEZYXWVU",
         anydp = 100 -- the "any dp" value (prevents nearest 1e-100, though)

function sufficate(sequence args)
    bigatom size = ba_new(substitute(args[1],",",""))
    for a=2 to length(args) do
        sequence sa = scanf(args[a],"%f")
        args[a] = iff(length(sa)=1?sa[1][1]:anydp)
    end for
    integer sgn = ba_compare(size,0),
            dp = iff(length(args)>=2?args[2]:anydp),    -- decimal places
            bd = iff(length(args)>=3?args[3]:10)        -- 2 or 10
    atom ip = power(10,dp) -- (inverted precision)
    size = ba_abs(size)
    if dp<0 then size = ba_round(size,ip) end if
    string suffix
    if ba_compare(size,1e100)>0 then
        size = ba_div(size,1e100)
        suffix = "googol"
    else
        integer factor = iff(bd=2?1024:1000), fdx = 0
        while fdx<length(suffixes) do
            bigatom rsize = ba_div(size,factor)
            if dp<0 then rsize = ba_round(rsize,ip) end if
            if ba_compare(rsize,1)<0 then exit end if
            size = rsize
            fdx += 1
        end while
        suffix = iff(fdx=0?"":suffixes[fdx]&iff(bd=2?"i":""))
    end if
    string fmt = iff(dp<0 or dp=anydp?"%0B":sprintf("%%0.%dB",dp))
    if sgn=-1 then size = ba_mul(size,sgn) end if
    return {args[1],ba_sprintf(fmt, size) & suffix}
end function

constant tests = split("""
 87,654,321
-998,877,665,544,332,211,000 3
+112,233 0
 16,777,216 1
456,789,100,000,000
456,789,100,000,000 2 10
456,789,100,000,000 5 2
456,789,100,000.000e+00 0 10
 16777216 , 2
1.2e101
347,344 -2
10
""",'\n')

for t in tests do
    printf(1,"%30s : %s\n",sufficate(split(t)))
end for
